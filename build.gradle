def vJavaLang = '1.8'
def logbackVersion = '1.1.3'
def slf4jVersion = '1.7.11'

def javaProjects = [project(':common')]


configure(allprojects) {
    apply plugin: 'idea'

    version = '7.1.1'

    idea.module {
        excludeDirs += [
                file('bin'),      // containing build artifacts from Eclipse (if any)
                file('work'),     // containing transient artifacts used by embedded Tomcat
                file('logs'),     // containing app logs written by embedded Tomcat
                file('data'),     // containing logs and data written by elasticsearch
                file('.settings') // containing intentionally checked-in Eclipse metadata
        ]
    }
}

configure(javaProjects) {
    apply plugin: 'java'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    targetCompatibility = vJavaLang
    sourceCompatibility = vJavaLang

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        compile ("ch.qos.logback:logback-classic:${logbackVersion}")
        compile ("ch.qos.logback:logback-core:${logbackVersion}")
        compile ("org.slf4j:slf4j-api:${slf4jVersion}")
    }

    configurations {
        // replaced with jcl-over-slf4j
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        // replaced with log4j-over-slf4j
        all*.exclude group: 'log4j', module: 'log4j'
    }

}

repositories {
    mavenLocal()
    jcenter()
}
